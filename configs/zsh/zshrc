#!/bin/zsh
alias recovery="if test -f ~/recovery; then rm -f ~/recovery && echo 'Recovery mode disabled'; else touch ~/recovery && echo 'Recovery mode enabled'; fi"
alias tmux-recovery="if test -f ~/tmux-recovery; then rm -f ~/tmux-recovery && echo 'Tmux recovery mode disabled'; else touch ~/tmux-recovery && echo 'Tmux recovery mode enabled'; fi"
if test -f ~/recovery; then
  return
fi
DOTFILES_NAME=.dots

if ! test -f ~/.zinit/bin/zinit.zsh; then
  echo "Installing zinit..."
  mkdir -p ~/.zinit
  git clone https://github.com/zdharma-continuum/zinit ~/.zinit/bin
fi

source ~/.zinit/bin/zinit.zsh

# force to use emacs-style keymapping
bindkey -e

autoload -Uz compinit
autoload -Uz bashcompinit && bashcompinit
typeset -i updated_at=$(date +'%j' -r ~/.zcompdump 2>/dev/null || stat -f '%Sm' -t '%j' ~/.zcompdump 2>/dev/null)
if test $(date +'%j') != $updated_at; then
  compinit -i
else
  compinit -C -i
fi
autoload -Uz add-zsh-hook

setopt append_history # append history list to history file, rather than replace it
setopt hist_ignore_dups # don't record a command that just run
setopt hist_save_no_dups # don't save duplicate entries to history file
setopt hist_ignore_all_dups # remove older duplicate entries from history
setopt hist_reduce_blanks # remove superfluous blanks from history items
setopt hist_ignore_space # skip command with leading space
setopt share_history # share history between different instances of the shell

setopt auto_cd # cd by typing directory name if it's not a command
setopt correct # autocorrect commands

setopt auto_list # automatically list choices on ambiguous completion
setopt auto_menu # automatically use menu completion
setopt always_to_end # move cursor to end if word had one match
setopt complete_in_word # allow completion from within a word/phrase

setopt ignore_eof # don't trigger logout / exit on C-d
bindkey '^D' end-of-line

zstyle ':completion:*' menu select # select completions with arrow keys
zstyle ':completion:*' group-name '' # group results by category
zstyle ':completion:::::' completer _expand _complete _ignored _approximate # enable approximate matches for completion
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' accept-exact '*(N)' # Speedup path completion

# Cache expensive completions
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path ~/.cache/zsh

ZSH_TMUX_AUTOSTART=true
ZSH_TMUX_AUTOCONNECT=false
ZSH_TMUX_UNICODE=true

# Workaround for tmux with zinit: https://github.com/zdharma-continuum/zinit/issues/503
ZSH_TMUX_FIXTERM=false

# early set the variables for terminal and tmux to use
. $HOME/$DOTFILES_NAME/configs/zsh/setup

EXPLIST=""
get_variant() {
  if ! test -f ~/.explist; then
    printf 'z'
    return
  fi
  if test -z "$EXPLIST"; then
    EXPLIST="$(cat ~/.explist)"
  fi
  variant="$(printf '%s' "$EXPLIST" | grep "\b$1\b=" | tail -n1 | cut -d'=' -f2)"
  if test -z "$variant"; then
    printf 'z'
  else
    printf '%s' "$variant"
  fi
}

exp_is() {
  variant="$(get_variant "$2")"
  printf '%s' "$1" | grep -q "$variant"
}

zinit snippet OMZP::brew

allow_terminal() {
  case "$TERM_PROGRAM" in
    "")
      # kitty does not set the TERM_PROGRAM
      return 0
      ;;
    ghostty|Apple_Terminal)
      return 0
      ;;
    *)
      break
      ;;
  esac

  return 1
}

allow_tmux() {
  # running inside vim terminal
  if test -n "$VIM"; then
    return 1
  fi

  # nested tmux or zellij
  if test -n "$TMUX" || test -n "$ZELLIJ"; then
    return 1
  fi

  if ! allow_terminal; then
    return 1
  fi

  if exp_is aA tmux; then
    return 1
  fi

  return 0
}

if allow_tmux; then
  if test -n "$(command -v zellij)" && exp_is bB zellij; then
    ZELLIJ_AUTO_ATTACH=true
    ZELLIJ_AUTO_EXIT=true
    . <(zellij setup --generate-auto-start zsh)
  elif test -n "$(command -v tmux)"; then
    zinit ice lucid wait"0"
    zinit snippet OMZ::plugins/tmux
  fi
else
  zinit light michaelaquilina/zsh-you-should-use
  zinit light aloxaf/fzf-tab
  zinit ice lucid depth"1" as"program" pick"bin/git-fuzzy"
  zinit light bigH/git-fuzzy

  zinit light zsh-users/zsh-autosuggestions
  zinit ice lucid depth"1" blockf
  zinit light zsh-users/zsh-completions
  zinit light zsh-users/zsh-history-substring-search
  zinit light zdharma-continuum/fast-syntax-highlighting

  zinit snippet OMZP::common-aliases
  zinit ice lucid as"completion"
  zinit snippet OMZP::docker/completions/_docker
  zinit ice lucid as"completion"
  zinit snippet https://raw.githubusercontent.com/containers/podman/main/completions/zsh/_podman
  zinit snippet OMZL::git.zsh
  zinit snippet OMZP::git
  zinit snippet OMZP::git-auto-fetch
  zinit snippet OMZP::gitignore
  zinit ice lucid as"completion"
  zinit snippet OMZP::httpie/_httpie
  zinit snippet OMZP::npm
fi

. <(starship init zsh)

. $HOME/$DOTFILES_NAME/configs/zsh/aliases
. $HOME/$DOTFILES_NAME/configs/zsh/functions
. $HOME/$DOTFILES_NAME/configs/zsh/fuzzy
. $HOME/$DOTFILES_NAME/configs/zsh/variables
. $HOME/$DOTFILES_NAME/configs/zsh/hooks
for hook in $HOME/$DOTFILES_NAME/configs/zsh/preexec/*; do
  . "$hook"
done

# Use personalize settings if found as well
test -f ~/.personal && source ~/.personal

# Use fzf if found
test -f ~/.fzf.zsh && source ~/.fzf.zsh

# Use GPT command lookup
if test -n "$(command -v gpt)" && gpt --uuid >/dev/null 2>&1; then
  . <(gpt --zsh)
fi

# Setup hstr
if test -n "$(command -v hstr)"; then
  setopt histignorespace           # skip cmds w/ leading space from history
  export HSTR_CONFIG=hicolor       # get more colors
  bindkey -s "\C-r" "\C-a hstr -- \C-j"     # bind hstr to Ctrl-r (for Vi mode check doc)
  export HSTR_TIOCSTI=y
fi

# Setup github cli
if test -n "$(command -v gh)"; then
  . <(gh completion -s zsh)
fi

# Setup deno
if test -n "$(command -v deno)" && deno completions bash >/dev/null 2>&1; then
  . <(deno completions bash)
fi
